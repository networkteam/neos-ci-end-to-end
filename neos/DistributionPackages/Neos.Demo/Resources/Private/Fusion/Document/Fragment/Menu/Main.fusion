prototype(Neos.Demo:Document.Fragment.Menu.Main) < prototype(Neos.Fusion:Component) {
    root = ${site}
    menuItems = Neos.Neos:MenuItems

    renderer = afx`
        <nav class="navbar navbar-static-top navbar-default first-level-navigation" role="navigation">
            <div class="navbar-header">
                <button type="button" class="navbar-toggle" data-toggle="collapse" data-target=".navbar-main-collapse">
                    <span class="sr-only">Toggle navigation</span>
                    <span class="icon-bar"></span>
                    <span class="icon-bar"></span>
                    <span class="icon-bar"></span>
                </button>

                <Neos.Neos:NodeLink node={props.root}>
                    {StaticResource.content('Neos.Demo', 'Public/Images/logo.svg', true)}
                </Neos.Neos:NodeLink>
            </div>

            <div class="collapse navbar-collapse navbar-main-collapse">
                <ul class="nav nav-pills nav-justified">
                    <Neos.Fusion:Loop items={props.menuItems}>
                        <li class={item.state}>
                            <Neos.Neos:NodeLink node={item.node}>{item.label}</Neos.Neos:NodeLink>
                            <ul
                                @if.has={item.subItems && (item.state != 'normal')}
                                class="second-level-sub-navigation nav nav-justified visible-xs"
                            >
                                <Neos.Fusion:Loop items={item.subItems} itemName="item">
                                    <li class={item.state}>
                                        <Neos.Neos:NodeLink node={item.node}>{item.label}</Neos.Neos:NodeLink>
                                    </li>
                                </Neos.Fusion:Loop>
                            </ul>
                        </li>
                    </Neos.Fusion:Loop>
                </ul>
            </div>
        </nav>
    `

    @cache {
        mode = 'cached'
        entryIdentifier {
            documentNode = ${documentNode}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
        }
    }
}
