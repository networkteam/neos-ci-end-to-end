prototype(Neos.Demo:Document.Fragment.Menu.Meta) < prototype(Neos.Fusion:Component) {
    menuItems = Neos.Neos:MenuItems {
        entryLevel = 2
        maximumLevels = 1
        startingPoint = ${q(site).children('metamenu').get(0)}
    }

    renderer = afx`
        <nav @if.hasMenuItems={props.menuItems} class="nav" role="navigation">
            <ul class="nav nav-pills">
                <Neos.Fusion:Loop items={props.menuItems}>
                    <li class={item.state}>
                        <Neos.Neos:NodeLink node={item.node}>{item.label}</Neos.Neos:NodeLink>
                    </li>
                </Neos.Fusion:Loop>
            </ul>
        </nav>
    `

    @cache {
        mode = 'cached'
        entryIdentifier {
            documentNode = ${documentNode}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
        }
    }
}
